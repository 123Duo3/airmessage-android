//Load keystore
def keystorePropertiesFile = rootProject.file(".signing/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

apply plugin: "com.android.application"

android {
	signingConfigs {
		config {
			storeFile file(keystoreProperties["storeFile"])
			storePassword keystoreProperties["storePassword"]
			keyAlias keystoreProperties["keyAlias"]
			keyPassword keystoreProperties["keyPassword"]
		}
	}
	compileSdkVersion 30
	buildToolsVersion "29.0.3"
	defaultConfig {
		applicationId "me.tagavari.airmessage"
		minSdkVersion 23
		targetSdkVersion 30
		versionName "3.1.4"
		versionCode 110

		resConfigs "en", "fr", "ja"

		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
	}
	buildTypes {
		debug {
			ext.enableCrashlytics = false

			applicationIdSuffix ".debug"
			versionNameSuffix "-DEBUG"

			resValue "string", "applicationId", "me.tagavari.airmessage.debug"

			/* minifyEnabled true
			shrinkResources true

			useProguard true
			proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro" */
		}
		release {
			/* minifyEnabled true
			shrinkResources true

			useProguard true
			proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro" */

			resValue "string", "applicationId", "me.tagavari.airmessage"

			signingConfig signingConfigs.config
		}
	}
	lintOptions {
		abortOnError false
	}
	aaptOptions {
		noCompress "tflite"
	}
	productFlavors {
	}
	packagingOptions {
		exclude "META-INF/DEPENDENCIES"
		exclude "META-INF/LICENSE"
	}
	dexOptions {
		javaMaxHeapSize "5g"
	}
	testOptions {
		unitTests.includeAndroidResources = true

	}
	compileOptions {
		coreLibraryDesugaringEnabled true
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
}

dependencies {
	coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:1.1.1"

	//implementation fileTree(include: ["*.jar"], dir: "libs")
	testImplementation "junit:junit:4.13.1"
	testImplementation "com.google.truth:truth:1.1"
	testImplementation "com.google.truth.extensions:truth-java8-extension:1.1"
	testImplementation "androidx.test:core:1.3.0"
	testImplementation "org.mockito:mockito-core:3.6.28"

	androidTestImplementation "androidx.test.ext:truth:1.3.0"
	androidTestImplementation "androidx.test:runner:1.3.0"
	androidTestImplementation "androidx.test:rules:1.3.0"

	//Android extensions
	implementation "androidx.core:core:1.3.2"
	implementation "androidx.preference:preference:1.1.1"
	implementation "androidx.browser:browser:1.3.0"
	implementation "androidx.exifinterface:exifinterface:1.3.2"
	implementation "androidx.security:security-crypto:1.0.0-rc04"

	//Material components
	implementation "com.google.android.material:material:1.3.0"

	//Android architecture components
	def lifecycle_version = "2.3.0"

	// ViewModel
	implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycle_version"
	// LiveData
	implementation "androidx.lifecycle:lifecycle-livedata:$lifecycle_version"
	// Lifecycles only (without ViewModel or LiveData)
	implementation "androidx.lifecycle:lifecycle-runtime:$lifecycle_version"

	//ReactiveX
	implementation "io.reactivex.rxjava3:rxandroid:3.0.0"
	implementation "io.reactivex.rxjava3:rxjava:3.0.10"

	//Layouts and views
	implementation "androidx.constraintlayout:constraintlayout:2.0.4"
	implementation "androidx.viewpager2:viewpager2:1.0.0"
	implementation "com.google.android:flexbox:2.0.1"

	implementation "nl.dionsegijn:konfetti:1.2.6"
	implementation "com.github.chrisbanes:PhotoView:2.3.0"

	//ML Kit
	implementation "com.google.mlkit:smart-reply:16.1.1"

	//Firebase
	implementation platform("com.google.firebase:firebase-bom:26.5.0")

	implementation "com.google.firebase:firebase-core"
	implementation "com.google.firebase:firebase-messaging"
	implementation "com.google.firebase:firebase-auth"
	implementation "com.google.firebase:firebase-analytics"
	implementation "com.google.firebase:firebase-crashlytics"

	//GMS
	implementation "com.google.android.gms:play-services-maps:17.0.0"
	implementation "com.google.android.gms:play-services-location:17.1.0"
	implementation "com.google.android.gms:play-services-auth:19.0.0"

	//About libraries
	implementation "com.mikepenz:aboutlibraries-core:$aboutlibs_version"
	implementation "com.mikepenz:aboutlibraries:$aboutlibs_version"

	//Tools
	implementation "com.googlecode.ez-vcard:ez-vcard:0.11.2"

	//Backend
	//implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
	implementation "org.bouncycastle:bcprov-jdk15to18:1.68"

	implementation "com.github.bumptech.glide:glide:4.12.0"
	annotationProcessor "com.github.bumptech.glide:compiler:4.12.0"
	implementation "jp.wasabeef:glide-transformations:4.3.0"

	implementation "com.google.android.exoplayer:exoplayer-core:2.13.1"
	implementation "com.google.android.exoplayer:exoplayer-ui:2.13.1"

	implementation "org.jsoup:jsoup:1.13.1"

	implementation "com.klinkerapps:android-smsmms:5.2.6"

	implementation "org.java-websocket:Java-WebSocket:1.5.1"
	implementation "com.otaliastudios:transcoder:0.9.1"
}

repositories {
	maven {
		url "https://maven.google.com"
		// Alternative URL is "https://dl.google.com/dl/android/maven2/"
	}
	maven {
		url "https://jitpack.io"
	}
}

apply plugin: "com.google.gms.google-services"
apply plugin: "com.google.firebase.crashlytics"
apply plugin: "kotlin-android"
apply plugin: "com.mikepenz.aboutlibraries.plugin"