//Load keystore
def keystorePropertiesFile = rootProject.file(".signing/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

apply plugin: "com.android.application"
apply plugin: "io.fabric"

android {
	signingConfigs {
		config {
			storeFile file(keystoreProperties["storeFile"])
			storePassword keystoreProperties["storePassword"]
			keyAlias keystoreProperties["keyAlias"]
			keyPassword keystoreProperties["keyPassword"]
		}
	}
	compileSdkVersion 29
	buildToolsVersion "29.0.2"
	defaultConfig {
		applicationId "me.tagavari.airmessage"
		minSdkVersion 23
		targetSdkVersion 29
		versionName "0.5"
		versionCode 60

		resConfigs "en", "fr", "ja"
	}
	buildTypes {
		debug {
			ext.enableCrashlytics = false

			applicationIdSuffix ".debug"
			versionNameSuffix "-DEBUG"

			resValue "string", "applicationId", "me.tagavari.airmessage.debug"

			/* minifyEnabled true
			shrinkResources true

			useProguard true
			proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro" */
		}
		release {
			/* minifyEnabled true
			shrinkResources true

			useProguard true
			proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro" */

			resValue "string", "applicationId", "me.tagavari.airmessage"

			signingConfig signingConfigs.config
		}
	}
	lintOptions {
		abortOnError false
	}
	aaptOptions {
		noCompress "tflite"
	}
	productFlavors {
	}
	packagingOptions {
		exclude "META-INF/DEPENDENCIES"
		exclude "META-INF/LICENSE"
	}
	dexOptions {
		javaMaxHeapSize "5g"
	}
	testOptions {
		unitTests.returnDefaultValues = true
	}
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
}

dependencies {
	//implementation fileTree(include: ["*.jar"], dir: "libs")
	testImplementation "junit:junit:4.12"

	//Android extensions
	implementation "androidx.preference:preference:1.1.0"
	implementation "androidx.browser:browser:1.2.0"
	implementation "androidx.exifinterface:exifinterface:1.1.0"

	//Material components
	implementation "com.google.android.material:material:1.1.0-beta02"

	//Android architecture components
	implementation "androidx.lifecycle:lifecycle-extensions:2.1.0"

	//Backports
	implementation "org.lukhnos:nnio:0.2"

	//Layouts and views
	implementation "androidx.constraintlayout:constraintlayout:1.1.3"
	implementation "androidx.viewpager2:viewpager2:1.0.0"
	implementation "com.google.android:flexbox:2.0.0"

	implementation "com.pnikosis:materialish-progress:1.7"
	implementation "nl.dionsegijn:konfetti:1.1.3"
	implementation "de.hdodenhof:circleimageview:3.0.1"
	implementation "com.github.chrisbanes:PhotoView:2.3.0"

	//Services
	implementation "com.google.firebase:firebase-core:17.2.2"
	implementation "com.google.firebase:firebase-ml-natural-language:22.0.0"
	implementation "com.google.firebase:firebase-ml-natural-language-smart-reply-model:20.0.7"
	implementation "com.crashlytics.sdk.android:crashlytics:2.10.1"
	implementation "com.google.android.gms:play-services-maps:17.0.0"
	implementation "com.google.android.gms:play-services-location:17.0.0"

	//Tools
	implementation "com.googlecode.ez-vcard:ez-vcard:0.10.5"
	implementation "id.zelory:compressor:2.1.0"

	//Backend
	implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
	implementation "org.bouncycastle:bcprov-jdk15on:1.64"

	implementation "com.github.bumptech.glide:glide:4.11.0"
	annotationProcessor "com.github.bumptech.glide:compiler:4.11.0"
	implementation "jp.wasabeef:glide-transformations:4.1.0"

	implementation "com.google.android.exoplayer:exoplayer-core:2.11.1"
	implementation "com.google.android.exoplayer:exoplayer-ui:2.11.1"

	implementation "io.github.ponnamkarthik:richlinkpreview:1.0.9"
	implementation "org.jsoup:jsoup:1.12.1"

	implementation "com.klinkerapps:android-smsmms:5.2.5"
}

repositories {
	maven {
		url "https://maven.google.com"
		// Alternative URL is "https://dl.google.com/dl/android/maven2/"
	}
	maven {
		url "https://jitpack.io"
	}
}

apply plugin: "com.google.gms.google-services"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
