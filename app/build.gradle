//Load keystore
def keystorePropertiesFile = rootProject.file(".signing/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

apply plugin: "com.android.application"
apply plugin: "io.fabric"

android {
	signingConfigs {
		config {
			storeFile file(keystoreProperties["storeFile"])
			storePassword keystoreProperties["storePassword"]
			keyAlias keystoreProperties["keyAlias"]
			keyPassword keystoreProperties["keyPassword"]
		}
	}
	compileSdkVersion 29
	buildToolsVersion '29.0.2'
	defaultConfig {
		applicationId "me.tagavari.airmessage"
		minSdkVersion 23
		targetSdkVersion 29
		targetSdkVersion 29
		versionName "0.4.9"
		versionCode 57

		resConfigs "en", "fr", "ja"
	}
	buildTypes {
		debug {
			ext.enableCrashlytics = false

			/* minifyEnabled true
			shrinkResources true

			useProguard true
			proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro" */
		}
		release {
			/* minifyEnabled true
			shrinkResources true

			useProguard true
			proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro" */

			signingConfig signingConfigs.config
		}
	}
	lintOptions {
		abortOnError false
	}
	aaptOptions {
		noCompress "tflite"
	}
	productFlavors {
	}
	packagingOptions {
		exclude "META-INF/DEPENDENCIES"
		exclude "META-INF/LICENSE"
	}
	dexOptions {
		javaMaxHeapSize "5g"
	}
	testOptions {
		unitTests.returnDefaultValues = true
	}
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
}

dependencies {
	//implementation fileTree(include: ["*.jar"], dir: "libs")
	testImplementation "junit:junit:4.12"

	//Android extensions
	def androidXVersion = "1.0.0"
	implementation "androidx.preference:preference:$androidXVersion"
	implementation "androidx.browser:browser:$androidXVersion"

	//Material components
	implementation "com.google.android.material:material:1.1.0-alpha09"

	//Android architecture components
	implementation "androidx.lifecycle:lifecycle-extensions:2.0.0"

	//Backports
	implementation "org.lukhnos:nnio:0.2"

	//Layouts and views
	implementation "androidx.constraintlayout:constraintlayout:1.1.3"
	implementation "com.google.android:flexbox:1.1.0"

	implementation "com.pnikosis:materialish-progress:1.7"
	implementation "de.hdodenhof:circleimageview:3.0.0"
	implementation "nl.dionsegijn:konfetti:1.1.3"

	//Services
	implementation "com.google.firebase:firebase-core:17.0.1"
	implementation "com.google.firebase:firebase-ml-natural-language:20.0.2"
	implementation "com.google.firebase:firebase-ml-natural-language-smart-reply-model:20.0.2"
	implementation "com.crashlytics.sdk.android:crashlytics:2.10.1"

	//Helper
	implementation "com.takisoft.preferencex:preferencex-ringtone:1.0.0"

	//Backend
	implementation "org.jetbrains.kotlin:kotlin-stdlib:1.3.41"
	implementation "org.bouncycastle:bcprov-jdk15on:1.62"

	implementation "com.github.bumptech.glide:glide:4.9.0"
	implementation "jp.wasabeef:glide-transformations:4.0.1"
	annotationProcessor "com.github.bumptech.glide:compiler:4.9.0"

	implementation "io.github.ponnamkarthik:richlinkpreview:1.0.9"
	implementation "org.jsoup:jsoup:1.12.1"
}

repositories {
	maven {
		url "https://maven.google.com"
		// Alternative URL is "https://dl.google.com/dl/android/maven2/"
	}
	maven {
		url "https://jitpack.io"
	}
}

apply plugin: "com.google.gms.google-services"