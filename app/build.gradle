//Load keystore
def keystorePropertiesFile = rootProject.file(".signing/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

apply plugin: "com.android.application"
apply plugin: "io.fabric"

android {
	signingConfigs {
		config {
			storeFile file(keystoreProperties["storeFile"])
			storePassword keystoreProperties["storePassword"]
			keyAlias keystoreProperties["keyAlias"]
			keyPassword keystoreProperties["keyPassword"]
		}
	}
	compileSdkVersion 28
	buildToolsVersion "28.0.3"
	defaultConfig {
		applicationId "me.tagavari.airmessage"
		minSdkVersion 23
		targetSdkVersion 28
		versionCode 40
		versionName "0.2.4"

		resConfigs "en", "fr"
	}
	buildTypes {
		debug {
			ext.enableCrashlytics = false

			/* minifyEnabled true
			shrinkResources true

			useProguard true
			proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro" */
		}
		release {
			/* minifyEnabled true
			shrinkResources true

			useProguard true
			proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro" */

			signingConfig signingConfigs.config
		}
	}
	lintOptions {
		abortOnError false
	}
	productFlavors {
	}
	packagingOptions {
		exclude "META-INF/DEPENDENCIES"
		exclude "META-INF/LICENSE"
	}
	dexOptions {
		javaMaxHeapSize "5g"
	}
	testOptions {
		unitTests.returnDefaultValues = true
	}
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
}

dependencies {
	//implementation fileTree(include: ["*.jar"], dir: "libs")
	testImplementation "junit:junit:4.12"

	//Android extensions
	def androidXVersion = "1.0.0"
	implementation "androidx.preference:preference:$androidXVersion"
	implementation "androidx.browser:browser:$androidXVersion"

	//Material components
	implementation "com.google.android.material:material:1.0.0"

	//Android architecture components
	implementation "androidx.lifecycle:lifecycle-extensions:2.0.0"

	//Backports
	implementation "net.sourceforge.streamsupport:android-retrostreams:1.6.3"
	implementation "org.lukhnos:nnio:0.2"

	//Layouts
	implementation "androidx.constraintlayout:constraintlayout:1.1.3"
	implementation "com.google.android:flexbox:1.0.0"

	implementation "com.pnikosis:materialish-progress:1.7"
	implementation "de.hdodenhof:circleimageview:2.2.0"
	implementation "nl.dionsegijn:konfetti:1.1.2"

	//Services
	implementation "com.google.firebase:firebase-core:16.0.4"
	implementation "com.crashlytics.sdk.android:crashlytics:2.9.5"

	//Helper
	implementation "com.takisoft.preferencex:preferencex-ringtone:1.0.0-alpha2"

	//Backend
	implementation "org.jetbrains.kotlin:kotlin-stdlib:1.2.60"
	implementation "org.bouncycastle:bcprov-jdk15on:1.60"

	implementation "com.github.bumptech.glide:glide:4.8.0"
	implementation "jp.wasabeef:glide-transformations:3.3.0"
	//annotationProcessor "com.github.bumptech.glide:compiler:4.7.1"
}

repositories {
	maven {
		url "https://maven.google.com"
		// Alternative URL is "https://dl.google.com/dl/android/maven2/"
	}
}

apply plugin: "com.google.gms.google-services"