//Load keystore
def keystorePropertiesFile = rootProject.file(".signing/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

buildscript {
	repositories {
		maven {
			url 'https://maven.fabric.io/public'
		}
	}
	dependencies {
		classpath 'io.fabric.tools:gradle:1.25.1'
	}
}

android {
	signingConfigs {
		config {
			storeFile file(keystoreProperties['storeFile'])
			storePassword keystoreProperties['storePassword']
			keyAlias keystoreProperties['keyAlias']
			keyPassword keystoreProperties['keyPassword']
		}
	}
	compileSdkVersion 27
	buildToolsVersion '27.0.3'
	defaultConfig {
		applicationId 'me.tagavari.airmessage'
		minSdkVersion 23
		targetSdkVersion 27
		versionCode 18
		versionName "0.1.9.1.5"
	}
	buildTypes {
		release {
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
			signingConfig signingConfigs.config
		}
		debug {
			ext.enableCrashlytics = false
		}
	}
	lintOptions {
		abortOnError false
	}
	productFlavors {
	}
	packagingOptions {
		exclude 'META-INF/DEPENDENCIES'
		exclude 'META-INF/LICENSE'
	}
	dexOptions {
		javaMaxHeapSize "5g"
	}
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
}

dependencies {
	def supportLibraryVersion = "27.1.0"

	implementation fileTree(include: ['*.jar'], dir: 'libs')
	testImplementation "junit:junit:4.12"
	implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
	implementation "com.android.support:preference-v7:$supportLibraryVersion"
	implementation "com.android.support:preference-v14:$supportLibraryVersion"
	implementation "com.android.support:cardview-v7:$supportLibraryVersion"
	implementation "com.android.support:design:$supportLibraryVersion"
	implementation "com.android.support:customtabs:$supportLibraryVersion"
	implementation "com.android.support.constraint:constraint-layout:1.0.2"
	implementation "com.google.android:flexbox:0.3.1"

	//classpath 'com.google.gms:google-services:3.1.0'
	implementation "com.google.firebase:firebase-core:12.0.0"
	implementation("com.crashlytics.sdk.android:crashlytics:2.9.1@aar") {
		transitive = true
	}
	//releaseCompile "com.google.firebase:firebase-crash:$firebaseVersion"

	implementation "com.pascalwelsch.compositeandroid:activity:27.0.0"
	implementation "org.java-websocket:Java-WebSocket:1.3.7"
	implementation "com.pnikosis:materialish-progress:1.7"
	implementation "in.srain.cube:grid-view-with-header-footer:1.0.12"
	implementation "de.hdodenhof:circleimageview:2.2.0"
	implementation "nl.dionsegijn:konfetti:1.1.1"
	implementation 'com.takisoft.fix:preference-v7-ringtone:27.1.0.0'

	debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
	releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'

	implementation "org.jetbrains.kotlin:kotlin-stdlib:1.2.0"
}

repositories {
	maven {
		url 'https://maven.google.com'
		// Alternative URL is 'https://dl.google.com/dl/android/maven2/'
	}
}

apply plugin: 'com.google.gms.google-services'